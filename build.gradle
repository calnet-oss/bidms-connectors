version projectVersion
group projectGroup

apply plugin: "groovy"
apply plugin: "maven-publish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for defaultCentral_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("defaultCentral_proxy_url")) {
            maven {
                url project.property("defaultCentral_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        else {
            jcenter()
        }
}

jar {
    from { ["LICENSE.txt", "NOTICE_THIRD_PARTY.txt"] }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
    from sourceSets.main.allJava
    from { ["LICENSE.txt", "NOTICE_THIRD_PARTY.txt"] }
}

task javadocJar (type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    from { ["LICENSE.txt", "NOTICE_THIRD_PARTY.txt"] }
}

dependencies {
    implementation "org.codehaus.groovy:groovy:3.0.11"

    testImplementation "org.spockframework:spock-core:2.1-groovy-3.0"
    testImplementation "cglib:cglib-nodep:3.3.0"
    implementation "org.slf4j:slf4j-api:1.7.36"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:2.18.0"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.18.0"

    implementation "org.springframework.ldap:spring-ldap-core:2.4.1"

    testImplementation "io.github.bkoehm:apacheds-embedded:0.5"
    testImplementation "org.apache.commons:commons-pool2:2.11.1"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// publishing with './gradlew publish'
publish.dependsOn assemble, sourceJar, javadocJar
publishToMavenLocal.dependsOn assemble, sourceJar, javadocJar
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId project.name
            version projectVersion
            groupId projectGroup
            artifact sourceJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }

    repositories {
        maven {
            // Put the property settings in ~/.gradle/gradle.properties for
            // default_deploy_url, calnet_deploy_lib_repo,
            // default_deploy_username and default_deploy_password.
            url "${project.property('default_deploy_url')}/${calnet_deploy_lib_repo}"
            credentials {
                username project.property("default_deploy_user")
                password project.property("default_deploy_password")
            }
        }
    }
}
